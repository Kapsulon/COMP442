<START> ::= <prog>


<prog> ::= <classList> <funcDefList> 'main' <funcBody>


<classList> ::= <classDecl> <classList>
<classList> ::= EPSILON

<funcDefList> ::= <funcDef> <funcDefList>
<funcDefList> ::= EPSILON


<classDecl> ::= 'class' 'id' <classInheritOpt> '{' <classMemberList> '}' ';'


<classInheritOpt> ::= 'inherits' 'id' <classInheritTail>
<classInheritOpt> ::= EPSILON

<classInheritTail> ::= ',' 'id' <classInheritTail>
<classInheritTail> ::= EPSILON


<classMemberList> ::= <visibility> <memberDecl> <classMemberList>
<classMemberList> ::= EPSILON


<visibility> ::= 'public'
<visibility> ::= 'private'


<memberDecl> ::= 'id' '(' <fParams> ')' ':' <funcDeclTail>
<memberDecl> ::= <type_no_id> 'id' <varArrayList> ';'


<funcDeclTail> ::= <type> ';'
<funcDeclTail> ::= 'void' ';'


<funcDef> ::= <funcHead> <funcBody> ';'


<funcHead> ::= 'id' <funcHeadTail>

<funcHeadTail> ::= 'sr' 'id' '(' <fParams> ')' ':' <funcHeadReturn>
<funcHeadTail> ::= '(' <fParams> ')' ':' <funcHeadReturn>

<funcHeadReturn> ::= <type>
<funcHeadReturn> ::= 'void'


<funcBody> ::= <localDeclOpt> 'do' <stmtList> 'end'


<localDeclOpt> ::= 'local' <varDeclList>
<localDeclOpt> ::= EPSILON

<varDeclList> ::= <varDecl> <varDeclList>
<varDeclList> ::= EPSILON


<varDecl> ::= <type> 'id' <varArrayList> ';'


<type> ::= 'integer'
<type> ::= 'float'
<type> ::= 'id'

<type_no_id> ::= 'integer'
<type_no_id> ::= 'float'


<varArrayList> ::= <arraySize> <varArrayList>
<varArrayList> ::= EPSILON


<arraySize> ::= '[' <arraySizeTail>

<arraySizeTail> ::= 'intNum' ']'
<arraySizeTail> ::= ']'


<fParams> ::= <type> 'id' <fParamsArrayList> <fParamsTail>
<fParams> ::= EPSILON

<fParamsArrayList> ::= <arraySize> <fParamsArrayList>
<fParamsArrayList> ::= EPSILON

<fParamsTail> ::= ',' <type> 'id' <fParamsArrayList> <fParamsTail>
<fParamsTail> ::= EPSILON


<aParams> ::= <expr> <aParamsTail>
<aParams> ::= EPSILON

<aParamsTail> ::= ',' <expr> <aParamsTail>
<aParamsTail> ::= EPSILON


<stmtList> ::= <statement> <stmtList>
<stmtList> ::= EPSILON


<statement> ::= 'id' <idNestList> <statementIdTail>
<statement> ::= 'if' '(' <expr> ')' 'then' <statBlock> 'else' <statBlock> ';'
<statement> ::= 'while' '(' <expr> ')' <statBlock> ';'
<statement> ::= 'read' '(' <variable> ')' ';'
<statement> ::= 'write' '(' <expr> ')' ';'
<statement> ::= 'return' '(' <expr> ')' ';'


<statementIdTail> ::= <variableIndexList> <assignOp> <expr> ';'
<statementIdTail> ::= '(' <aParams> ')' ';'


<statBlock> ::= 'do' <stmtList> 'end'
<statBlock> ::= <statement>
<statBlock> ::= EPSILON


<assignOp> ::= '='


<variable> ::= 'id' <idNestList> <variableIndexList>


<idNestList> ::= <idNest> <idNestList>
<idNestList> ::= EPSILON

<idNest> ::= <idNestTail> '.' 'id'

<idNestTail> ::= <variableIndexList>
<idNestTail> ::= '(' <aParams> ')'


<variableIndexList> ::= <indice> <variableIndexList>
<variableIndexList> ::= EPSILON

<indice> ::= '[' <arithExpr> ']'


<expr> ::= <arithExpr> <exprRelTail>

<exprRelTail> ::= <relOp> <arithExpr>
<exprRelTail> ::= EPSILON


<arithExpr> ::= <term> <arithExprTail>

<arithExprTail> ::= <addOp> <term> <arithExprTail>
<arithExprTail> ::= EPSILON


<term> ::= <factor> <termTail>

<termTail> ::= <multOp> <factor> <termTail>
<termTail> ::= EPSILON


<factor> ::= 'id' <idNestList> <factorIdTail>
<factor> ::= 'intNum'
<factor> ::= 'floatNum'
<factor> ::= '(' <arithExpr> ')'
<factor> ::= 'not' <factor>
<factor> ::= <sign> <factor>


<factorIdTail> ::= '(' <aParams> ')'
<factorIdTail> ::= <variableIndexList>


<addOp> ::= '+'
<addOp> ::= '-'
<addOp> ::= 'or'

<multOp> ::= '*'
<multOp> ::= '/'
<multOp> ::= 'and'

<relOp> ::= 'eq'
<relOp> ::= 'neq'
<relOp> ::= 'lt'
<relOp> ::= 'gt'
<relOp> ::= 'leq'
<relOp> ::= 'geq'

<sign> ::= '+'
<sign> ::= '-'
